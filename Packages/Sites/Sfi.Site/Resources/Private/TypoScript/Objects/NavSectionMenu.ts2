prototype(S:NavSectionMenu) < prototype(T:Array) {
	@process.tmpl = ${'<div class="NavSectionMenu">' + value + '</div>'}
	list = T:Collection {
		collection = ${q(site).children('main').find('[instanceof Sfi.Site:NavSection]').get()}
		itemRenderer = T:Tag {
			tagName = 'a'
			attributes.class = 'NavSectionMenu-item Button Dog'
			attributes.title = ${node.properties.title}
			attributes.href = NodeUri {
				node = ${site}
				@process.addAnchor = ${value + '#' + node.properties.navTitle}
			}
			content = ${node.properties.title}
		}
		@cache {
			mode = 'cached'
			entryIdentifier {
				node = ${node}
			}
			entryTags {
				1 = ${'NodeType_Sfi.Site:NavSection'}
			}
		}
	}

	langLink = T:Tag {
		@context.targetLang = ${Array.first(node.context.dimensions.language) == 'ru' ? 'en' : 'ru'}
		tagName = 'a'
		attributes.class = 'NavSectionMenu-item Button Button--alt Dog'
		attributes.title = ${node.properties.title}
		attributes.href = NodeUri {
			node = ${q(documentNode).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).count() > 0 ?
			q(documentNode).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).get(0) :
			q(site).context({targetDimensions: {language: targetLang}, dimensions: {language: [targetLang]}}).get(0)}
		}
		content = ${targetLang == 'en' ? 'English' : 'Русский'}
	}
}
